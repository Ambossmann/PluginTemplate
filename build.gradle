plugins {
	id 'java'
	id 'eclipse'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(jvmversion)

repositories {
	mavenCentral()
	maven {
		url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

    content {
      includeGroup 'org.bukkit'
      includeGroup 'org.spigotmc'
    }
	}
	mavenLocal()
}

configurations {
	spigot.extendsFrom compileOnly
}

dependencies {
	spigot("org.spigotmc:spigot:" + mcversion + "-R0.1-SNAPSHOT")
	compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
	annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
}

eclipse {
	classpath {
		downloadJavadoc = true
		downloadSources = true
	}
}

tasks.register('updateSpigot', Copy) {
	group 'spigot'
	from configurations.spigot
	include "spigot-*.jar"
	into "$projectDir/server"
}

tasks.register('movePlugin', Copy) {
	group 'spigot'
	dependsOn tasks.named('assemble')
	from "$projectDir/build/libs/" + project.name + "-" + version + ".jar"
	into "$projectDir/server/plugins"
}

tasks.register('startSpigot', JavaExec) {
	group 'spigot'
	javaLauncher = javaToolchains.launcherFor {
		languageVersion = JavaLanguageVersion.of(jvmversion)
	}
	dependsOn tasks.named('movePlugin')
	dependsOn tasks.named('updateSpigot')
	classpath files("/server/spigot-" + mcversion + "-R0.1-SNAPSHOT.jar")
	workingDir = "/server"
}

tasks.register('fixEclipseClasspath') {
	group 'ide'
	dependsOn tasks.named('cleanEclipseClasspath')
	dependsOn tasks.named('eclipseClasspath')
}

tasks.named('eclipseClasspath').get().mustRunAfter('cleanEclipseClasspath')
